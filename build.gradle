
group 'dk.bankdata.gradle.swagger'
version '2.0.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile(group: 'io.swagger.core.v3', name: 'swagger-jaxrs2', version: '2.0.2')
    compileOnly (group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1')
    testCompile(group: 'junit', name: 'junit', version: '4.12')
    testCompile(group: 'io.openapitools.jackson.dataformat', name: 'jackson-dataformat-hal', version: '1.0.4')
    testCompile(group: 'io.openapitools.hal', name: 'swagger-hal', version: '1.0.4')
    testCompile(group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                //authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                //authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name 'Gradle Swagger Plugin'
                description 'Gradle plugin for generating OpenAPI specification using Swagger'
                packaging 'jar'
                url 'https://github.com/Bankdata/gradle-swagger-plugin'
                scm {
                    connection 'scm:git:https://github.com/Bankdata/gradle-swagger-plugin.git'
                    developerConnection 'scm:git:https://github.com/Bankdata/gradle-swagger-plugin.git'
                    url 'https://github.com/Bankdata/gradle-swagger-plugin'
                    tag 'HEAD'
                }
                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'lange'
                        name 'Thor Anker Kvisg√•rd Lange'
                        email 'tel@bankdata.dk'
                        organization 'Bankdata'
                        organizationUrl 'https://www.bankdata.dk'
                    }
                }
            }
        }
    }
}
